<!doctype html>
<html>
  <head>
    <meta charset="utf=8">
    <title>Convolution</title>
    <script type="text/javascript" src="../../processing.js"></script>
    <link type="text/css" rel="stylesheet" href="../style.css"/>
  </head>
  <body>
    <h4><a href="http://processingjs.org/">Processing.js</a> > <a href="index.html">Topic Demos</a> > Image Processing</h4>
    <h1>Convolution</h1>
    <p>This code was updated from the Java source to work with Processing.js asynchronous image loading.</p>
    <p>by <a href="http://www.shiffman.net">Daniel Shiffman</a>. Applys a convolution matrix to a portion of the index. Move mouse to apply filter to different parts of the image.</p>
    <p><a href="http://processing.org/learning/topics/convolution.html"><b>Original Processing.org Example:</b> Convolution</a></p>
    <script id='pjs_code' type="application/processing">
      /* @pjs preload="data/end.jpg"; */
      
      PImage img;
      int w = 80;
      
      // It's possible to convolve the image with 
      // many different matrices
      
        float[][] matrix = { { -1, -1, -1 },
                             { -1,  9, -1 },
                             { -1, -1, -1 } }; 
      
      void setup() {
        size(200, 200);
        frameRate(30);
        img = loadImage("data/end.jpg");
      }
      
      void draw() {
        // We're only going to process a portion of the image
        // so let's set the whole image as the background first
        image(img,0,0);
        // Where is the small rectangle we will process
        int xstart = constrain(mouseX-w/2,0,img.width);
        int ystart = constrain(mouseY-w/2,0,img.height);
        int xend = constrain(mouseX+w/2,0,img.width);
        int yend = constrain(mouseY+w/2,0,img.height);
        int matrixsize = 3;
        loadPixels();
        // Begin our loop for every pixel
        for (int x = xstart; x < xend; x++) {
          for (int y = ystart; y < yend; y++ ) {
            color c = convolution(x,y,matrix,matrixsize,img);
            int loc = x + y*img.width;
            pixels[loc] = c;
          }
        }
        updatePixels();
      }
      
      color convolution(int x, int y, float[][] matrix,int matrixsize, PImage img)
      {
        float rtotal = 0.0;
        float gtotal = 0.0;
        float btotal = 0.0;
        int offset = matrixsize / 2;
        for (int i = 0; i < matrixsize; i++){
          for (int j= 0; j < matrixsize; j++){
            // What pixel are we testing
            int xloc = x+i-offset;
            int yloc = y+j-offset;
            int loc = xloc + img.width*yloc;
            // Make sure we haven't walked off our image, we could do better here
            loc = constrain(loc,0,img.pixels.length-1);
            // Calculate the convolution
            rtotal += (red(img.pixels[loc]) * matrix[i][j]);
            gtotal += (green(img.pixels[loc]) * matrix[i][j]);
            btotal += (blue(img.pixels[loc]) * matrix[i][j]);
          }
        }
        // Make sure RGB is within range
        rtotal = constrain(rtotal,0,255);
        gtotal = constrain(gtotal,0,255);
        btotal = constrain(btotal,0,255);
        // Return the resulting color
        return color(rtotal,gtotal,btotal);
      }
    </script>
    <canvas></canvas>
    <pre id="show_code"></pre>
    <script type="text/javascript">
      var pre = '<b>// All Examples Written by <a href="http://reas.com/">Casey Reas</a> and <a href="http://benfry.com/">Ben Fry</a><br>// unless otherwise stated.</b><br>';
      document.getElementById('show_code').innerHTML = pre + document.getElementById('pjs_code').innerHTML.replace(/</g,"&lt;").replace(/>/g,"&gt;").replace(/\n {6}/g,'<br>');
    </script>
  </body>
</html>
