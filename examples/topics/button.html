<!doctype html>
<html>
  <head>
    <meta charset="utf=8">
    <title>Button</title>
    <script type="text/javascript" src="../../processing.js"></script>
    <link type="text/css" rel="stylesheet" href="../style.css"/>
  </head>
  <body>
    <h4><a href="http://processingjs.org/">Processing.js</a> > <a href="index.html">Topic Demos</a> > GUI</h4>
    <h1>Button</h1>
    <p>Click on one of the colored shapes in the center of the image to change the color of the background.</p>
    <p><a href="http://processing.org/learning/topics/button.html"><b>Original Processing.org Example:</b> Button</a></p>
    <script id='pjs_code' type="application/processing">
      int rectX, rectY;      // Position of square button
      int circleX, circleY;  // Position of circle button
      int rectSize = 50;     // Diameter of rect
      int circleSize = 53;   // Diameter of circle
      color rectColor, circleColor, baseColor;
      color rectHighlight, circleHighlight;
      color currentColor;
      boolean rectOver = false;
      boolean circleOver = false;
      
      void setup()
      {
        size(200, 200);
        smooth();
        rectColor = color(0);
        rectHighlight = color(51);
        circleColor = color(255);
        circleHighlight = color(204);
        baseColor = color(102);
        currentColor = baseColor;
        circleX = width/2+circleSize/2+10;
        circleY = height/2;
        rectX = width/2-rectSize-10;
        rectY = height/2-rectSize/2;
        ellipseMode(CENTER);
      }
      
      void draw()
      {
        update(mouseX, mouseY);
        background(currentColor);
        
        if(rectOver) {
          fill(rectHighlight);
        } else {
          fill(rectColor);
        }
        stroke(255);
        rect(rectX, rectY, rectSize, rectSize);
        
        if(circleOver) {
          fill(circleHighlight);
        } else {
          fill(circleColor);
        }
        stroke(0);
        ellipse(circleX, circleY, circleSize, circleSize);
      }
      
      void update(int x, int y)
      {
        if( overCircle(circleX, circleY, circleSize) ) {
          circleOver = true;
          rectOver = false;
        } else if ( overRect(rectX, rectY, rectSize, rectSize) ) {
          rectOver = true;
          circleOver = false;
        } else {
          circleOver = rectOver = false;
        }
      }
      
      void mousePressed()
      {
        if(circleOver) {
          currentColor = circleColor;
        }
        if(rectOver) {
          currentColor = rectColor;
        }
      }
      
      boolean overRect(int x, int y, int width, int height) 
      {
        if (mouseX >= x && mouseX <= x+width && 
            mouseY >= y && mouseY <= y+height) {
          return true;
        } else {
          return false;
        }
      }
      
      boolean overCircle(int x, int y, int diameter) 
      {
        float disX = x - mouseX;
        float disY = y - mouseY;
        if(sqrt(sq(disX) + sq(disY)) < diameter/2 ) {
          return true;
        } else {
          return false;
        }
      }
    </script>
    <canvas></canvas>
    <pre id="show_code"></pre>
    <script type="text/javascript">
      var pre = '<b>// All Examples Written by <a href="http://reas.com/">Casey Reas</a> and <a href="http://benfry.com/">Ben Fry</a><br>// unless otherwise stated.</b><br>';
      document.getElementById('show_code').innerHTML = pre + document.getElementById('pjs_code').innerHTML.replace(/</g,"&lt;").replace(/>/g,"&gt;").replace(/\n {6}/g,'<br>');
    </script>
  </body>
</html>
