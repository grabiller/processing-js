<!doctype html>
<html>
  <head>
    <meta charset="utf=8">
    <title>CharactersStrings</title>
    <script type="text/javascript" src="../../processing.js"></script>
    <link type="text/css" rel="stylesheet" href="../style.css"/>
  </head>
  <body>
    <h4><a href="http://processingjs.org/">Processing.js</a> > <a href="index.html">Basic Demos</a> > Data</h4>
    <h1>CharactersStrings</h1>
    <p>Click on the image to give it focus and then type letters to shift the location of the image. Characters are typographic symbols such as A, d, and %. The character datatype, abbreviated as char, stores letters and 
symbols in the Unicode format, a coding system developed to support a variety of world languages. Characters are distinguished from other symbols by putting them between single quotes ('P'). A string is a sequence of characters. A string is noted by surrounding a group of letters with double quotes ("Processing"). Chars and strings are most often used with the keyboard methods, to display text to the screen, and to load images or files.</p>
    <p>
      <a href="http://processing.org/learning/basics/charactersstrings.html"><b>Original Processing.org Example:</b> CharactersStrings</a><br>
      <script type="application/processing">
        PImage frog;
        PFont fontA;
        int lettersize = 90;
        int xoffset;
        char letter;

        void setup() 
        {
          size(200, 200);
          fontA = loadFont("Arial"); 
          textFont(fontA); 
          textSize(lettersize);
            
          // The String datatype must be capitalized because it is a complex datatype.
          // A String is actually a class with its own methods, some of which are
          // featured below.
          String name= "rathausFrog";
          String extension = ".jpg";
          int nameLength = name.length();
          println("The length of " + name + " is " + nameLength + ".");
          name = name.concat(extension);
          nameLength = name.length();
          println("The length of " + name + " is " + nameLength + ".");

          // The parameter for the loadImage() method must be a string
          // This line could also be written "frog = loadImage("rathausFrog.jpg");
          frog = loadImage(name);
        }

        void draw() 
        {
          background(51); // Set background to dark gray
          
          image(frog, xoffset, 0);
          
          // Draw an X
          line(0, 0, width, height);  
          line(0, height, width, 0); 
          
          // Get the width of the letter
          int letterWidth = int(fontA.width(letter) * lettersize);
              
          // Draw the letter to the center of the screen
          text(letter, width/2-letterWidth/2, height/2);
        }

        void keyPressed()
        {
          // The variable "key" always contains the value of the most recent key pressed.
          // If the key is an upper or lowercase letter between 'A' and 'z'
          // the image is shifted to the corresponding value of that key
          if(key >= 'A' && key <= 'z') {
            letter = char(key);
            // Scale the values to numbers between 0 and 100
            float scale = 100.0/57.0;
            int temp = int((key - 'A') * scale);
            // Set the offset for the image
            xoffset = temp;
            println(key);
          }
        }
      </script>
      <canvas></canvas>
    </p>
    <pre></pre>
    <script type="text/javascript">
      var pre = '<b>// All Examples Written by <a href="http://reas.com/">Casey Reas</a> and <a href="http://benfry.com/">Ben Fry</a><br>// unless otherwise stated.</b>';
      document.getElementsByTagName('pre')[0].innerHTML = pre + document.getElementsByTagName('script')[1].innerHTML.replace(/</g,"&lt;").replace(/>/g,"&gt;").replace(/\n {8}/g,'<br>');
    </script>
  </body>
</html>
